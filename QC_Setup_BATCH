@echo off
REM Updated by Chris Garcia 9/10/14
REM **********************************************************************************************

REM Sets the location of the two property files
set riginfo_properties="C:\opt\RW9\jboss\4.0.5.GA\server\default\conf\riginfo.properties"
set rigwatch_properties="C:\opt\RW9\jboss\4.0.5.GA\server\default\conf\rigwatch.properties"
REM Sets the location of the mySQL application
set mysql_dir="c:\Program Files\canrig\Rigwatch\MySQL\bin\mysql"
REM Sets the entry intended for the host file
set qc_host=qc-ftp	192.168.2.142
REM Sets the Service Name for JBOSS
set service=JBOSS405

REM Starts a new log file entry section
	echo -----------------------------Script Run On: %date% %time%------------------------------ >> %cd%\log.txt 

	goto :check_Permissions
:main
set next=:service_stop

REM Delayed Expansion will cause variables to be expanded at execution time rather than at parse time
	setlocal EnableDelayedExpansion

REM Stops the service
:service_stop
set next=:ri_properties
	echo Attempting to stop %service%
	echo [%date% %time%] Attempting to stop %service% >> %cd%\log.txt
	sc query %service% | find "RUNNING"
	if %errorLevel% == 0 (net stop %service%
	goto %next%) else (echo [%date% %time%] %service% is not in the RUNNING state >> %cd%\log.txt)
echo JBOSS is not in the RUNNING state
	goto :exit_choice

REM Updates the riginfo.properties file
:ri_properties
set next=:rw_properties
	echo Updating riginfo property file...
	echo [%date% %time%] Editing the riginfo property files to nabors network >> %cd%\log.txt	
	if exist %riginfo_properties% (del "%riginfo_properties%"
	echo remoteURL=http://12.96.17.180/RigInventoryServer/riginfo>> %riginfo_properties%
	echo dbServerName=localhost>> %riginfo_properties%
	echo dbPassword=0EE3227016E7DDACD9177530B2354D96>> %riginfo_properties%
	echo dbUsername=rigdata>> %riginfo_properties%
	echo dbName=rigdata>> %riginfo_properties%
	echo dbPort=3306>> %riginfo_properties%
	echo encrypedLog=0>> %riginfo_properties%
	echo checkFrequency=24>> %riginfo_properties%
	echo guidFile=/opt/RW9/rig_guid.dat>> %riginfo_properties%
		
	echo [%date% %time%] Remote URL set to http://12.96.17.180/RigInventoryServer/riginfo >> %cd%\log.txt
	echo [%date% %time%] CheckFrequency set to 24 >> %cd%\log.txt
	goto %next%
	)
	echo The riginfo_properties file does not exist
	echo [%date% %time%] %riginfo_properties% does not exist >> %cd%\log.txt
	goto :exit_choice

REM Updates the rigwatch.properties file
:rw_properties
set next=:service_start
	if exist %rigwatch_properties% (del "%rigwatch_properties%"
	echo Updating rigwatch property file...
	echo [%date% %time%] Editing the rigwatch property files to nabors network >> %cd%\log.txt	
	echo QUERY_TIMEOUT=360>> %rigwatch_properties%
	echo hessian_url=http://12.96.17.180:9080/edfsoftwareupdate/instruction>> "%rigwatch_properties%"
	
	echo [%date% %time%] Hessian URL set to http://12.96.17.180:9080/edfsoftwareupdate/instruction >> %cd%\log.txt
	
	echo Property file updates complete	
	goto %next%
	)
	echo The rigwatch_properties file does not exist
	echo [%date% %time%] %rigwatch_properties% does not exist >> %cd%\log.txt
	goto :exit_choice

REM Starts the service
:service_start
set next=:mysql_updates
	echo Attempting to start %service%
	echo [%date% %time%] Attempting to start %service% >> %cd%\test_log.txt
	sc query %service% | find "STOPPED"
	if %errorLevel% == 0 (net start %service%
	goto %next%) else (echo [%date% %time%] %service% is not in the STOPPED state >> %cd%\log.txt)
echo JBOSS is not in the STOPPED state
	goto :exit_choice

REM Updates the mySQL database
:mysql_updates
set next=:check_Host_File
	echo ------------------------------
	echo Performing mySQL updates...
REM Removes all slaves from the RigWatch config table
	echo [%date% %time%] Deleting all slaves from RigWatch config table >> %cd%\log.txt
	%mysql_dir% -urigdata -prigdata rigwatch -e "delete from rigwatch.rigwatch_config where framework_id like 'Slave%';"

REM Updates all RIGREPORTDATA entries to use hostname FTP.mywells.com	
	echo [%date% %time%] Updating Databse to point to QC FTP Server >> %cd%\log.txt
	%mysql_dir% -urigdata -prigdata rigdata -e "Update rigdata.FTP_login_info set HOSTNAME='FTP.mywells.com' where TYPE='RIGREPORTDATA';"

REM Updates the password used for all FTP.mywells.com hostname entries
	echo [%date% %time%] Setting HOSTNAME to "FTP.mywells.com" >> %cd%\log.txt	
	%mysql_dir% -urigdata -prigdata rigdata -e "Update rigdata.FTP_login_info set PASSWORD='B24FE90194466DF939B76F3EF8509CFA' where HOSTNAME='FTP.mywells.com';"

REM Sets the ECM URL
	echo [%date% %time%] Updating ECM URL >> %cd%\log.txt	
	%mysql_dir% -urigdata -prigdata rigdata -e "update transmetainfo.dynamic_config  set PROPERTY_VALUE = 'http://12.96.17.180:9080/ECM/ecmService'      where  PROPERTY_NAME = 'ecm_url';"

REM Sets the OPC Client Version
	echo [%date% %time%] Updating OPC Client Version >> %cd%\log.txt	
	%mysql_dir% -urigdata -prigdata rigdata -e "update transmetainfo.dynamic_config set PROPERTY_VALUE = '3' where PROPERTY_NAME ='opc_client_version';"
	
	echo mySQL updates complete
	echo ------------------------------
	goto %next%

:restart	
REM Prompts the user that a restart is required and ask if they would like to restart now.
	echo ------------------------------
	echo A restart is required for changes to take effect

	SET /P ANSWER=Do you want to restart now (Y/N)?
	echo You chose: %ANSWER%
	if /i {%ANSWER%}=={y} (goto :yes)
	if /i {%ANSWER%}=={yes} (goto :yes)
	goto :exit

:yes
REM Restarts the computer
	echo Restarting...
	c:\windows\system32\shutdown.exe /r
	goto :exit


:exit_choice
REM Allows user to continue with the process or quit
	SET /P ANSWER=Do you want to continue with the next step of the process (Y/N)?
	echo You chose: %ANSWER%
	if /i {%ANSWER%}=={y} (goto %next%)
	if /i {%ANSWER%}=={yes} (goto %next%)
	goto :exit

:exit
REM Exits the batch file
	exit /b

:check_Host_File
set next=:restart
REM Checks if the host file has been updated and updates it if required
	echo Checking Host File...
	findstr /i %qc_host% "C:\Windows\System32\drivers\etc\hosts" >nul 2>&1
	if %errorLevel%==0 (
        echo Host file updated.
	echo [%date% %time%] Host file updated >> %cd%\log.txt
	goto %next%	
	) else (
        echo Host file not updated, Updating Host File
	echo %qc_host%>> "C:\Windows\System32\drivers\etc\HOSTS"
	echo [%date% %time%] Host file was not updated, updating host file >> %cd%\log.txt
	goto :check_Host_File
	)
	
:check_Permissions
set next=:main
REM Checks for administrative permissions
	echo Administrative permissions required. Detecting permissions...

	net session >nul 2>&1
	if %errorLevel% == 0 (
        echo Success: Administrative permissions confirmed.
		echo [%date% %time%] Success: Administrative permissions confirmed. >> %cd%\log.txt
	goto %next%
	) else (
        echo Failure: Current permissions inadequate.
		echo [%date% %time%] Failure: Current permissions inadequate. >> %cd%\log.txt
    )

    pause >nul
